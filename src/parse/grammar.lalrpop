use std::str::FromStr;

use ast;
use lex;
use token::Token;

grammar;

// Lexer integration
extern {
  type Location = usize;
  type Error = lex::LexError;

  enum Token {
    TYPE      => Token::Type,
    VAR       => Token::Var,
    FUNCTION  => Token::Function,
    BREAK     => Token::Break,
    OF        => Token::Of,
    END       => Token::End,
    IN        => Token::In,
    NIL       => Token::Nil,
    LET       => Token::Let,
    DO        => Token::Do,
    TO        => Token::To,
    FOR       => Token::For,
    WHILE     => Token::While,
    ELSE      => Token::Else,
    THEN      => Token::Then,
    IF        => Token::If,
    ARRAY     => Token::Array,
    ASSIGN    => Token::Assign,
    OR        => Token::Or,
    AND       => Token::And,
    GE        => Token::Ge,
    GT        => Token::Gt,
    LE        => Token::Le,
    LT        => Token::Lt,
    NEQ       => Token::Neq,
    EQ        => Token::Eq,
    DIV       => Token::Div,
    MUL       => Token::Mul,
    SUB       => Token::Sub,
    ADD       => Token::Add,
    DOT       => Token::Dot,
    LBRACE    => Token::LBrace,
    RBRACE    => Token::RBrace,
    LBRACK    => Token::LBrack,
    RBRACK    => Token::RBrack,
    LPAREN    => Token::LParen,
    RPAREN    => Token::RParen,
    LCOMMENT  => Token::LComment,
    RCOMMENT  => Token::RComment,
    SEMICOLON => Token::Semicolon,
    COLON     => Token::Colon,
    COMMA     => Token::Comma,
    INT       => Token::Int(<i32>),
    STR       => Token::Str(<String>),
    ID        => Token::Ident(<String>),
  }
}

// Comma-separated list of T
Comma<T>: Vec<T> = {
  <h: T?> <t: (COMMA <T>)*> => {
    match h {
    | None    => t,
    | Some(e) => {
      let mut v = vec![e];
      v.extend(t);
      v
    }}
  }
}

// Semicolon-separated list of T
Semicolon<T> : Vec<T> = {
  <h: T?> <t: (SEMICOLON <T>)*> => {
    match h {
    | None    => t,
    | Some(e) => {
      let mut v = vec![e];
      v.extend(t);
      v
    }}
  }
}

pub Program: ast::Exp = Stm;

Decs: Vec<ast::Dec> = {
  TypeDecRec,
  VarDecRec,
  FunDecRec,
}

// Mutually recursive type declaration
TypeDecRec: Vec<ast::Dec> = {

  // Recursive type declarations followed by var declarations
  <l: @L> <decs: TypeDec+> <r: @R> <rest: VarDecRec> => {
    let mut rest = rest;
    rest.insert(0, ast::Dec::Type(decs, ast::to_span(l, r)));
    rest
  },

  // Recursive type declarations followed by recursive function declarations
  <l: @L> <decs: TypeDec+> <r: @R> <rest: FunDecRec> => {
    let mut rest = rest;
    rest.insert(0, ast::Dec::Type(decs, ast::to_span(l, r)));
    rest
  },

  // Just recursive type declarations
  <l: @L> <decs: TypeDec+> <r: @R> => vec![ast::Dec::Type(decs, ast::to_span(l, r))],

}

TypeDec: ast::TypeDec = {

  // type type-id = ty
  <l: @L> TYPE <name: ID> EQ <ty: Type> <r: @R> => ast::TypeDec { name, ty, span: ast::to_span(l, r) },

}

Type: ast::Type = {

  // ty -> type-id
  <l: @L> <name: ID> <r: @R> => ast::Type::Name(name, ast::to_span(l, r)),

  // ty -> { tyfields }
  <l: @L> LBRACK <fields: Comma<FieldDec>> RBRACK <r: @R> => ast::Type::Rec(fields, ast::to_span(l, r)),

  // ty -> array of type-id
  <l: @L> ARRAY OF <name: ID> <r: @R> => ast::Type::Arr(name, ast::to_span(l, r)),

}

FieldDec: ast::FieldDec = {

  // tyfields -> id : type-id
  <l: @L> <name: ID> COLON <ty: ID> <r: @R> => ast::FieldDec { name, escape: false, ty, span: ast::to_span(l, r) },

}

VarDecRec: Vec<ast::Dec> = {

  <dec: VarDec> <rest: Decs> => {
    let mut rest = rest;
    rest.insert(0, dec);
    rest
  },

  <dec: VarDec> => vec![dec],

}

VarDec: ast::Dec = {

  // var id := exp
  <l: @L> VAR <name: ID> ASSIGN <init: Exp> <r: @R> => ast::Dec::Var { name, escape: false, ty: None, init, span: ast::to_span(l, r) },

  // var id: type-id := exp
  <l: @L> VAR <name: ID> COLON <ty: ID> ASSIGN <init: Exp> <r: @R> => ast::Dec::Var { name, escape: false, ty: Some(ty), init, span: ast::to_span(l, r) },

}

Var: ast::Var = {

  // Simple variable
  <l: @L> <name: ID> <r: @R> => ast::Var::Simple(name, ast::to_span(l, r)),

  ComplexVar,

}

// Refer to https://stackoverflow.com/questions/26968665/bison-shift-reduce-conflict-tiger-compiler
ComplexVar: ast::Var = {

  // Field lookup
  <l: @L> <name: Var> DOT <field: ID> <r: @R> => ast::Var::Field(Box::new(name), field, ast::to_span(l, r)),

  // Direct array indexing
  <l: @L> <name: ID> <rn: @R> LBRACE <idx: Exp> RBRACE <r: @R> => {
    let var = ast::Var::Simple(name, ast::to_span(l, rn));
    ast::Var::Index(Box::new(var), Box::new(idx), ast::to_span(l, r))
  },

  // Indirect array indexing
  <l: @L> <name: ComplexVar> LBRACE <idx: Exp> RBRACE <r: @R> => ast::Var::Index(Box::new(name), Box::new(idx), ast::to_span(l, r)),

}

FunDecRec: Vec<ast::Dec> = {

  <l: @L> <decs: FunDec+> <r: @R> <rest: TypeDecRec> => {
    let mut rest = rest;
    rest.insert(0, ast::Dec::Fun(decs, ast::to_span(l, r)));
    rest
  },

  <l: @L> <decs: FunDec+> <r: @R> <rest: VarDecRec> => {
    let mut rest = rest;
    rest.insert(0, ast::Dec::Fun(decs, ast::to_span(l, r)));
    rest
  },

  <l: @L> <decs: FunDec+> <r: @R> => vec![ast::Dec::Fun(decs, ast::to_span(l, r))],

}

FunDec: ast::FunDec = {

  // fundec -> function id ( tyfields ) = exp
  <l: @L> FUNCTION <name: ID> LPAREN <args: Comma<FieldDec>> RPAREN EQ <body: Stm> <r: @R> => {
    ast::FunDec { name, args, rets: None, body, span: ast::to_span(l, r) }
  },

  // fundec -> function id ( tyfields ) : type-id = exp
  <l: @L> FUNCTION <name: ID> LPAREN <args: Comma<FieldDec>> RPAREN COLON <rets: ID> EQ <body: Stm> <r: @R> => {
    ast::FunDec { name, args, rets: Some(rets), body, span: ast::to_span(l, r) }
  },

}

// Refer to http://www.parsifalsoft.com/ifelse.html
Stm: ast::Exp = {

  // Statements where at least one [if ... then] doesn't have an else block
  OpenStm,

  // Statements where all [if ... then] have an else block
  ClosedStm,

}

OpenStm: ast::Exp = {

  // If statement with no else
  <l: @L> IF <guard: Exp> THEN <then: Stm> <r: @R> => {
    ast::Exp::If { guard: Box::new(guard), then: Box::new(then), or: None, span: ast::to_span(l, r) }
  },

  // If statement with else
  <l: @L> IF <guard: Exp> THEN <then: ClosedStm> ELSE <or: OpenStm> <r: @R> => {
    ast::Exp::If { guard: Box::new(guard), then: Box::new(then), or: Some(Box::new(or)), span: ast::to_span(l, r) }
  },

  // While statement
  <l: @L> WHILE <guard: Exp> DO <body: OpenStm> <r: @R> => {
    ast::Exp::While { guard: Box::new(guard), body: Box::new(body), span: ast::to_span(l, r) }
  },

  // For statement
  <l: @L> FOR <name: ID> ASSIGN <lo: Exp> TO <hi: Exp> DO <body: OpenStm> <r: @R> => {
    ast::Exp::For { name, escape: false, lo: Box::new(lo), hi: Box::new(hi), body: Box::new(body), span: ast::to_span(l, r) }
  },

}

ClosedStm: ast::Exp = {

  // While statement
  <l: @L> WHILE <guard: Exp> DO <body: ClosedStm> <r: @R> => {
    ast::Exp::While { guard: Box::new(guard), body: Box::new(body), span: ast::to_span(l, r) }
  },

  // For statement
  <l: @L> FOR <name: ID> ASSIGN <lo: Exp> TO <hi: Exp> DO <body: ClosedStm> <r: @R> => {
    ast::Exp::For { name, escape: false, lo: Box::new(lo), hi: Box::new(hi), body: Box::new(body), span: ast::to_span(l, r) }
  },

  // Assignment
  <l: @L> <name: Var> ASSIGN <exp: Exp> <r: @R> => {
    ast::Exp::Ass { name, exp: Box::new(exp), span: ast::to_span(l, r) }
  },

  Exp,

}

Exp: ast::Exp = {

  // If with else
  <l: @L> IF <guard: Exp> THEN <body: ClosedStm> ELSE <or: ClosedStm> <r: @R> => {
    ast::Exp::If { guard: Box::new(guard), then: Box::new(body), or: Some(Box::new(or)), span: ast::to_span(l, r) }
  },

  // Let statement
  <l: @L> LET <decs: Decs> IN <lb: @L> <body: Semicolon<Stm>> <rb: @R> END <r: @R> => {
    ast::Exp::Let { decs, body: Box::new(ast::Exp::Seq(body, ast::to_span(lb, rb))), span: ast::to_span(l, r) }
  },

  // Literal array
  <l: @L> <name: ID> <rn: @R> LBRACE <size: Exp> RBRACE OF <init: Or> <r: @R> => {
    ast::Exp::Arr { name, size: Box::new(size), init: Box::new(init), span: ast::to_span(l, r) }
  },

  Or,

}

Or: ast::Exp = {

  // Logical OR
  <l: @L> <lhs: Or> OR <rhs: And> <r: @R> => {
    ast::Exp::Bin { lhs: Box::new(lhs), op: ast::Binop::LOr, rhs: Box::new(rhs), span: ast::to_span(l, r) }
  },

  And,

}

And: ast::Exp = {

  // Logical AND
  <l: @L> <lhs: And> AND <rhs: Compare> <r: @R> => {
    ast::Exp::Bin { lhs: Box::new(lhs), op: ast::Binop::LAnd, rhs: Box::new(rhs), span: ast::to_span(l, r) }
  },

  Compare,

}

Compare: ast::Exp = {

  // Comparison operators
  <l: @L> <lhs: Term> <op: CompareOp> <rhs: Term> <r: @R> => {
    ast::Exp::Bin { lhs: Box::new(lhs), op, rhs: Box::new(rhs), span: ast::to_span(l, r) }
  },

  Term,

}

CompareOp: ast::Binop = {
  GE  => ast::Binop::Ge,
  GT  => ast::Binop::Gt,
  LE  => ast::Binop::Le,
  LT  => ast::Binop::Lt,
  NEQ => ast::Binop::Neq,
  EQ  => ast::Binop::Eq,
}

Term: ast::Exp = {

  // Addition and subtraction
  <l: @L> <lhs: Term> <op: TermOp> <rhs: Factor> <r: @R> => {
    ast::Exp::Bin { lhs: Box::new(lhs), op, rhs: Box::new(rhs), span: ast::to_span(l, r) }
  },

  Factor,

}

TermOp: ast::Binop = {
  ADD => ast::Binop::Add,
  SUB => ast::Binop::Sub,
}

Factor: ast::Exp = {

  // Multiplication and division
  <l: @L> <lhs: Factor> <op: FactorOp> <rhs: Unary> <r: @R> => {
    ast::Exp::Bin { lhs: Box::new(lhs), op, rhs: Box::new(rhs), span: ast::to_span(l, r) }
  },

  Unary,

}

FactorOp: ast::Binop = {
  DIV => ast::Binop::Div,
  MUL => ast::Binop::Mul,
}

Unary: ast::Exp = {

  // Unary negation
  <l: @L> SUB <exp: Unary> <r: @R> => ast::Exp::Neg(Box::new(exp), ast::to_span(l, r)),

  Atom,

}

Atom: ast::Exp = {

  // Variable
  <l: @L> <v: Var> <r: @R> => ast::Exp::Var(v, ast::to_span(l, r)),

  // Literal int
  <l: @L> <val: INT> <r: @R> => ast::Exp::Int(val, ast::to_span(l, r)),

  // Literal string
  <l: @L> <val: STR> <r: @R> => ast::Exp::Str(val, ast::to_span(l, r)),

  // Literal nil
  <l: @L> NIL <r: @R> => ast::Exp::Nil(ast::to_span(l, r)),

  // Literal record
  <l: @L> <name: ID> LBRACK <fields: Comma<Field>> RBRACK <r: @R> => ast::Exp::Rec { name, fields, span: ast::to_span(l, r) },

  // Function call
  <l: @L> <name: ID> LPAREN <args: Comma<Exp>> RPAREN <r: @R> => ast::Exp::Call { name, args, span: ast::to_span(l, r) },

  // Sequence
  <l: @L> LPAREN <exps: Semicolon<Stm>> RPAREN <r: @R> => ast::Exp::Seq(exps, ast::to_span(l, r)),

  // Break
  <@L> BREAK <@R> => ast::Exp::Break(ast::to_span(<>)),

}

Field: ast::Field = {
  <l: @L> <name: ID> EQ <exp: Exp> <r: @R> => ast::Field { name, exp: Box::new(exp), span: ast::to_span(l, r) },
}
